// For those who want the bleeding edge
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()

        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "gradle"
            url = "https://plugins.gradle.org/m2/"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0"
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "maven"

version = "${mc_version}-${enc_version}" + (System.getenv("BUILD_NUMBER") != null ? "." + System.getenv("BUILD_NUMBER") : "")
def mod_version = version
group= "exnihilocreatio" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "exnihilocreatio"

sourceCompatibility=1.8
targetCompatibility=1.8

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
	mappings = "${mappings_version}"
    useDepAts = true

    replace "@VERSION@", "$mod_version"
    replaceIn "ExNihiloCreatio.java"
}

repositories
{
    // Required for JEI and Mantle
    maven {
    	name "DVS1 Maven FS" 
    	url "http://dvs1.progwml6.com/files/maven" 
    }

    // Required for HWYLA
    maven {
        name "tehnut"
        url "http://tehnut.info/maven"
    }

    // Required for TheOneProbe
    maven {
        name "tterrag"
        url "http://maven.tterrag.com"
    }

    // Required for RedstoneFlux which is required for TheOneProbe
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }

    // crt
    maven {
        url "http://maven.blamejared.com"
    }

    // for code chicken libs
    maven {
        url "http://chickenbones.net/maven/"
    }

    // for forgelin
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies
{
    compileOnly "mezz.jei:jei_${mc_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"

    deobfCompile "slimeknights.mantle:Mantle:${mc_major}-${mantle_version}"
    deobfCompile "slimeknights:TConstruct:${mc_version}-${tconstruct_version}"

    compileOnly "mcp.mobius.waila:Hwyla:${hwyla_version}_${mc_version}:api"
    runtime "mcp.mobius.waila:Hwyla:${hwyla_version}_${mc_version}"

    compileOnly ("mcjty.theoneprobe:TheOneProbe-${mc_major}:${mc_major}-${top_version}:api") {
        exclude group: "cofh", module: "RedstoneFlux"
    }
    runtime ("mcjty.theoneprobe:TheOneProbe-${mc_major}:${mc_major}-${top_version}") {
        exclude group: "cofh", module: "RedstoneFlux"
    }

    // deobfCompile ("cofh:ThermalFoundation:1.12-2.3.6.16:universal") { exclude group: "mezz.jei", module: "jei_1.12" }
    deobfCompile ("CraftTweaker2:CraftTweaker2-MC1120-Main:${mc_major}-${ct2_version}")

    compileOnly "org.projectlombok:lombok:1.18.0"

    compile group: "net.shadowfacts", name: "Forgelin", version: "${forgelin_version}"
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
                
        // replace version and mcversion
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    from(sourceSets.api.output)

    manifest {
        attributes 'FMLAT': 'exnihilocreatio_at.cfg'
    }

}


// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}