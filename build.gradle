// For those who want the bleeding edge
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()

        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "gradle"
            url = "https://plugins.gradle.org/m2/"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0"
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "maven"

version = "1.12-0.3.5.1" + (System.getenv("BUILD_NUMBER") != null ? "." + System.getenv("BUILD_NUMBER") : "")
def mod_version = version
group= "exnihilocreatio" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "exnihilocreatio"

sourceCompatibility=1.8
targetCompatibility=1.8

minecraft {
    version = "1.12.2-14.23.5.2810"
    runDir = "run"
	mappings = "snapshot_20180814"
    useDepAts = true

    replace "@VERSION@", "$mod_version"
    replaceIn "ExNihiloCreatio.java"
}

repositories
{
    // Required for JEI and Mantle
    maven {
    	name "DVS1 Maven FS" 
    	url "http://dvs1.progwml6.com/files/maven" 
    }

    // Required for HWYLA
    maven {
        name "tehnut"
        url "http://tehnut.info/maven"
    }

    // Required for TheOneProbe
    maven {
        name "tterrag"
        url "http://maven.tterrag.com"
    }

    // Required for RedstoneFlux which is required for TheOneProbe
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }

    // crt
    maven {
        url "http://maven.blamejared.com"
    }

    // for code chicken libs
    maven {
        url "http://chickenbones.net/maven/"
    }

    // for forgelin
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies
{
    deobfCompile "mezz.jei:jei_1.12.2:4.11.0.206"

    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.39"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.12.0.115"

    compileOnly "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2:api"
    runtime "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"

    compileOnly ("mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.23-16:api") {
        exclude group: "cofh", module: "RedstoneFlux"
    }
    runtime ("mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.23-16") {
        exclude group: "cofh", module: "RedstoneFlux"
    }

    // deobfCompile ("cofh:ThermalFoundation:1.12-2.3.6.16:universal") { exclude group: "mezz.jei", module: "jei_1.12" }
    deobfCompile ('CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.9.487')

    compileOnly "org.projectlombok:lombok:1.18.0"

    compile group: "net.shadowfacts", name: "Forgelin", version: "1.8.2"
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
                
        // replace version and mcversion
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    from(sourceSets.api.output)

    manifest {
        attributes 'FMLAT': 'exnihilocreatio_at.cfg'
    }

}


// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}