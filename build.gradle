buildscript {
    dependencies {
        classpath 'org.codehaus.groovy:groovy-json:3.0.9'
    }
}

plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+mc" + project.minecraft_version
group = project.maven_group

repositories {
    mavenCentral()

    maven { url "https://maven.fabricmc.net/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.saps.dev/minecraft" }
    maven { url "https://maven.bai.lol/" }
    maven { url "https://maven.wispforest.io" }
    maven { url "https://maven.architectury.dev/" }
    maven {
        url "https://maven.blamejared.com"
        content {
            includeGroup "com.faux.ingredientextension"
        }
    }
    maven {
        url "http://maven.modmuss50.me/"
        allowInsecureProtocol = true
        content {
            includeGroup "TechReborn"
        }
    }
    maven {
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

// toggle this if you need to make a change to allow intellij to refresh
boolean nothing = true

var settings = new Object() {
    boolean wthit
    boolean megane
    boolean modmenu
    boolean rei
    boolean emi
    boolean theOneProbe
    boolean kubejs
    boolean techReborn
    boolean industrialRevolution
    boolean modernIndustrialization
    boolean jade
}
apply from: "config.gradle", to: settings

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version"
    modCompileOnly "dev.emi:emi-fabric:$emi_version:api"
    modCompileOnly "dev.latvian.mods:kubejs-fabric:$kubejs_version"
    modCompileOnly "com.terraformersmc:modmenu:$modmenu_version"

    include(implementation("com.github.LlamaLad7:MixinExtras:0.2.0-beta.9"))
    annotationProcessor("com.github.LlamaLad7:MixinExtras:0.2.0-beta.9")

    // Add generated data to runtime classpath (slightly hacky)
    runtimeOnly files("src/main/generated")
    modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:$cloth_config_version"

    if (settings.rei) {
        modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
        modRuntimeOnly "dev.architectury:architectury-fabric:$architectury_version"
    }
    if (settings.emi) {
        modRuntimeOnly "dev.emi:emi-fabric:$emi_version"
    }
    // Disabled: no 1.19.3 support
    //if (settings.kubejs) modRuntimeOnly "dev.latvian.mods:kubejs-fabric:$kubejs_version"
    if (settings.modmenu) modRuntimeOnly "com.terraformersmc:modmenu:$modmenu_version"
    if (settings.techReborn) modRuntimeOnly "TechReborn:TechReborn-1.19:$tech_reborn_version"
    // Disabled: Mojmap breaking shit
    //if (settings.modernIndustrialization) modRuntimeOnly "maven.modrinth:modern-industrialization:$modern_industrialization_version"
    if (settings.industrialRevolution) modRuntimeOnly "curse.maven:industrial-revolution-391708:$industrial_revolution_version"

    // WTHIT and Megane
    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-$wthit_version"
    if (settings.wthit) {
        modRuntimeOnly "mcp.mobius.waila:wthit:fabric-$wthit_version"
    }
    modCompileOnly "lol.bai.megane:megane-api:7.1.0"
    if (settings.megane) {
        modRuntimeOnly "lol.bai:badpackets:fabric-0.1.2"
        modRuntimeOnly "lol.bai.megane:megane-runtime:$megane_version"
        modRuntimeOnly "lol.bai.megane:megane-vanilla:$megane_version"
        modRuntimeOnly "lol.bai.megane:megane-api:$megane_version"
    }

    // The one probe
    modCompileOnly "mcjty.theoneprobe:theoneprobe-fabric:$theoneprobe_version"
    // Disabled: No 1.19.3 support
    //if (settings.theOneProbe) {
    //    modRuntimeOnly "mcjty.theoneprobe:theoneprobe-fabric:$theoneprobe_version"
    //    modRuntimeOnly "com.electronwill.night-config:toml:3.6.0"
    //}

    modCompileOnly "curse.maven:jade-324717:4434061"
    if (settings.jade) {
        modRuntimeOnly "curse.maven:jade-324717:4434061"
    }
}

loom {
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=fabricaeexnihilo"
            //vmArg "-Dfabric-api.datagen.strict-validation"

            ideConfigGenerated = true

            runDir "build/datagen/"
        }
    }

    // Apply datagen at runtime
    mods {
        fabricaeexnihilo {
            sourceSet sourceSets.main
            modFiles.from file("src/main/generated")
        }
    }
}

// Hack to copy dependency overrides to datagen
tasks.register('copyDatagenOverrides', Copy) {
    from "run/config/fabric_loader_dependencies.json"
    into "build/datagen/config"
}
clean.finalizedBy(tasks.copyDatagenOverrides)
tasks.runDatagen.dependsOn(tasks.copyDatagenOverrides)

// Delete datagen on clean
clean.delete("src/main/generated")


import groovy.json.JsonException
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each { File file ->
            try {
                file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            } catch (JsonException e) {
                throw new RuntimeException("Failed to minify json for $file", e)
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(17)
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    // Copy datagen to jar
    dependsOn tasks.runDatagen
    from("src/main/generated") {
        exclude "README.md"
        exclude ".cache"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}